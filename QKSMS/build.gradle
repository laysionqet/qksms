apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://clojars.org/repo/' }
}

android {
    flavorDimensions "analytics"

    productFlavors {
        withAnalytics {
            dimension "analytics"
        }
        noAnalytics {
            dimension "analytics"
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        disable 'InvalidPackage'
    }

    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        versionCode 127
        versionName "2.5.5"
        minSdkVersion 15
        targetSdkVersion 23
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'qksms-proguard.pro'
        }
        debug {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'qksms-proguard.pro'
        }
    }

    compileOptions {
        compileOptions.setEncoding("UTF-8")

        // Use Java 1.7, requires minSdk 8
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false

        // Translations are crowd-sourced
        disable 'MissingTranslation'
    }

}

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.2'
        classpath 'io.fabric.tools:gradle:1.21.5'
    }
}

String supportLibraryVersion = '23.4.0'
String leakCanaryVersion = '1.4-beta2'

dependencies {
    // Open source libraries
    compile 'com.mcxiaoke.volley:library:1.0.19'
    compile ('com.melnykov:floatingactionbutton:1.3.0') {
        exclude group: 'com.android.support'
    }
    compile 'com.pushbullet:android-extensions:1.0.4'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile ('com.klinkerapps:android-chips:1.0.0') {
        exclude group: 'com.android.support'
    }
    compile 'com.googlecode.libphonenumber:libphonenumber:6.2'
    compile ('com.nispok:snackbar:2.10.6') {
        exclude group: 'com.android.support'
    }
    compile 'com.github.lzyzsd:circleprogress:1.1.0'
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    compile 'com.vdurmont:emoji-java:1.1.1'
    compile 'me.leolin:ShortcutBadger:1.1.1'
    compile 'com.googlecode.ez-vcard:ez-vcard:0.9.6'
    compile 'com.google.code.gson:gson:2.4'
    compile ('com.koushikdutta.ion:ion:1.1.7') {
        exclude group: 'com.google.code.gson'
    }
    compile ('org.ligi:snackengage:0.4') {
        exclude group: 'com.android.support'
    }
    compile 'frankiesardo:icepick:3.2.0'
    provided 'frankiesardo:icepick-processor:3.2.0'

    // Google libraries
    compile 'com.android.support:multidex:1.0.1'
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile ("com.android.support:support-v13:$supportLibraryVersion") {
        exclude group: 'com.android.support'
    }
    compile ("com.android.support:recyclerview-v7:$supportLibraryVersion") {
        exclude group: 'com.android.support'
    }
    withAnalyticsCompile 'com.google.android.gms:play-services-analytics:8.4.0'

    // External services
    withAnalyticsCompile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
}
